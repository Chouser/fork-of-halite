expr = conditional
conditional = or | or '?' expr ':' conditional | or '=>' conditional
or = and | or '||' and
and = equality | and '&&' equality
equality = relational | relational ('==' | '!=') relational
relational = add | relational ('<' | '<=' | '>=' | '>') add
add = mult | add ('+' | '-') mult
mult = prefix | mult ('*' | '/' | '%') prefix
prefix = postfix | '!' prefix

postfix = primary | get-field | get-index | call-fn | call-method
get-field = postfix <'.'> symbol
get-index = postfix <'['> expr <']'>
call-fn = symbol <'('> (expr (<','> expr)* )? <')'>
call-method = postfix <'.'> symbol <'('> (expr (<','> expr)* )? <')'>

primary = int | symbol | string | typename | map | let | <'('> expr <')'>
map = <'{'> ( symbol <':'> expr ( <','> symbol <':'> expr )* )? <'}'>
let = <'{'> ( symbol <'='> expr <';'> )* expr <'}'>
int = "-"? #"\d+"
symbol = #"[a-zA-Z$][a-zA-Z0-9_$]*"
typename = <"<"> #"[a-zA-Z0-9./$]+" <">">
string = #"\"([^\"\\]|\\.)*\""
